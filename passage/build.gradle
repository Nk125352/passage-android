plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id "org.openapi.generator" version "6.3.0"
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    namespace 'id.passage.android'
    compileSdk 33

    defaultConfig {
        minSdk 28 // Minimum support for Credentials Manager - ie passkeys
        targetSdk 33
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main {
            kotlin {
                srcDir("$projectDir/generated/src/main/kotlin")
            }
        }
    }
}

dependencies {
    // Android core dependencies
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    // For testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    // For simplifying network requests
    implementation "com.squareup.okhttp3:okhttp:4.10.0"

    // For JSON serialization and parsing
    implementation "com.squareup.moshi:moshi:1.14.0"
    implementation "com.squareup.moshi:moshi-adapters:1.14.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.14.0"

    // For safely encrypting stored data
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    // For Android Credentials support
    implementation "androidx.credentials:credentials:1.2.0-alpha03"

    // Needed for credentials support from play services, for devices running
    // Android 13 (sdk 33) and below.
    implementation "androidx.credentials:credentials-play-services-auth:1.2.0-alpha03"
}

openApiGenerate {
    generatorName = "kotlin"
    remoteInputSpec = "https://api.swaggerhub.com/apis/passage_swagger/auth-gw/v1"
    outputDir = "$projectDir/generated".toString()
    apiPackage = "id.passage.android.api"
    modelPackage = "id.passage.android.model"
    configOptions = [
            useCoroutines: "true",
            "apiSuffix": "API",
            "packageName": "id.passage.client",
            "moshiCodeGen": "true"
    ]
}
